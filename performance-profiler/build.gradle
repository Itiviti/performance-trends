allprojects {
    apply plugin: 'java'
    version = '1.0-SNAPSHOT'
    group = 'com.ullink.performance.trends'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

dependencies {
    compile group: 'org.javassist', name: 'javassist', version: '3.18.2-GA'
    compile project('log-formatter')
    compile project('fast-logger')
    compile project('method-selector')
}

jar {
    /* Create fat jar (so that it can be applied to any app using using JVM option
    "-javaagent: /path/to/instrumentation.jar" without the need to add other libraries to the classpath manually) */
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    /* Add java agent specific pre-main to manifest file */
    manifest {
        attributes('Premain-Class': 'com.ullink.instrumentation.DurationAgent',
                'Implementation-Title': 'Ullink performance profiler agent',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Ullink')
    }
}